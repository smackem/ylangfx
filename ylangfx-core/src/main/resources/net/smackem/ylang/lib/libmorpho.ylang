/// dilate the given kernel `knl` (which should be binarized before).
/// grows non-black objects on black background by `radius` pixels.
/// returns the dilated kernel.
fn dilate(knl, radius) {
    out := kernel(knl.bounds)
    k := new_morpho_kernel(radius)
    for p in knl.bounds {
        out[p] = knl.convolve(p, k) > 0 ? 255 : 0
    }
    return out
}

/// erode the given kernel `knl` (which should be binarized before).
/// shrinks non-black objects on black background by `radius` pixels.
/// returns the eroded kernel.
fn erode(knl, radius) {
    out := kernel(knl.bounds)
    K := new_morpho_kernel(radius)
    for p in knl.bounds {
        out[p] = knl.convolve(p, K) < 255 ? 0 : 255
    }
    return out
}

/// creates a round kernel, where all elements within the given `radius`
/// are set to 1, all others to 0.
fn new_morpho_kernel(radius) {
    k := kernel(radius * 2 + 1, radius * 2 + 1, 0)
    center := radius;radius
    for p in k.bounds() {
        k[p] = hypot(p.x - center.x, p.y - center.y) <= radius ? 1 : 0
    }
    return k
}
