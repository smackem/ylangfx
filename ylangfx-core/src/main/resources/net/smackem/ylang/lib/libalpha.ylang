#include "libutil"

/// extracts all alpha values into a kernel with the dimensions
/// of the input image `inp`.
/// returns the new kernel.
/// useful for applying kernel operations on the alpha channel, which
/// can afterwards be merged back into the image with the `image_with_alpha` function.
fn alpha_kernel(img) {
    assert(img.type == Type_Image)
    k := kernel(img)
    for p in img.bounds {
        k[p] = img[p].a
    }
    return k
}

/// convolves the alpha channel of image `inp` with the kernel `k`
/// and returns a new image.
fn convolve_alpha(img, k) {
    assert(img.type == Type_Image)
    assert(k.type == Type_Kernel)
    out := image(img.bounds)
    kernel_sum := k.sum()
    for p in img.bounds {
        px := img[p]
        selection := img.select_alpha(p, k)
        selection_sum := selection.sum()
        alpha := kernel_sum != 0 ? selection_sum / kernel_sum : selection_sum
        out[p] = rgba(px.r, px.g, px.b, alpha)
    }
    return out
}

/// creates a new image with the same dimensions as image `img` and
/// kernel `alpha_kernel`, replacing the input image's alpha channel
/// with the values of `alpha_kernel`.
/// returns the new image.
fn image_with_alpha(img, alpha_kernel) {
    assert(img.type == Type_Image)
    assert(alpha_kernel.type == Type_Kernel)
    assert(alpha_kernel.bounds == img.bounds)
    out := image(img.bounds)
    for p in out.bounds {
        out[p] = img[p].a(alpha_kernel[p])
    }
    return out
}
